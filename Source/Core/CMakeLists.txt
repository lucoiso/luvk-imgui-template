# Author: Lucas Vilas-Boas
# Year: 2025
# Repo: https://github.com/lucoiso/luvk-imgui-template

SET(LIBRARY_NAME Core)

SET(${LIBRARY_NAME}_SOURCE_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src)
SET(${LIBRARY_NAME}_INCLUDE_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/)

FILE(GLOB_RECURSE SOURCE_FILES CONFIGURE_DEPENDS "${${LIBRARY_NAME}_SOURCE_BASE_DIRECTORY}/*.cpp")
FILE(GLOB_RECURSE HEADER_FILES CONFIGURE_DEPENDS "${${LIBRARY_NAME}_INCLUDE_BASE_DIRECTORY}/${LIBRARY_NAME}/**/*.hpp")

ADD_LIBRARY(${LIBRARY_NAME} ${SOURCE_FILES})
SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES LINKER_LANGUAGE CXX)

STRING(TOUPPER ${LIBRARY_NAME} BUILD_DLL_PREFIX)
GET_TARGET_PROPERTY(TARGET_TYPE ${LIBRARY_NAME} TYPE)

IF(TARGET_TYPE STREQUAL "SHARED_LIBRARY")
    IF(WIN32)
        TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PRIVATE "${BUILD_DLL_PREFIX}_API=__declspec(dllexport)")
        TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} INTERFACE "${BUILD_DLL_PREFIX}_API=__declspec(dllimport)")
    ELSE()
        TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PRIVATE "${BUILD_DLL_PREFIX}_API=__attribute__((visibility(\"default\")))")
        TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} INTERFACE "${BUILD_DLL_PREFIX}_API=")
    ENDIF()
ELSE()
    TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC "${BUILD_DLL_PREFIX}_API=")
ENDIF()

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PRIVATE ${${LIBRARY_NAME}_SOURCE_BASE_DIRECTORY})
TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PUBLIC ${${LIBRARY_NAME}_INCLUDE_BASE_DIRECTORY})

TARGET_LINK_LIBRARIES(${LIBRARY_NAME} PUBLIC
                      luvk
                      SDL3::SDL3
                      glm::glm
                      imgui
)